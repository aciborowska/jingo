### What is the problem

A path expression containing a nested positional predicate, which works on an in-memory node, fails on an identical node stored in the database.

### What did you expect

I expected a path expression on a structure to work uniformly whether it is in-memory or stored in the database.

### Describe how to reproduce or add a test

The following xqsuite test, adapted from @ChristianGruen's https://github.com/BaseXdb/basex/issues/1573, shows how the in-database function fails while the in-memory function works correctly.

```xquery
xquery version "3.1";

module namespace npt="http://exist-db.org/test/nested-positional-predicate";

declare namespace test="http://exist-db.org/xquery/xqsuite";

declare variable $npt:DATA :=
    document {
        <xml>
            <a>
                <b>A</b>
            </a>
            <a>
                <b>B</b>
                <c>correct</c>
            </a>
            <a>
                <b>B</b>
                <c>wrong</c>
            </a>
        </xml>
    };
    
declare
    %test:setUp
function npt:setup() {
    xmldb:create-collection("/db", "test"),
    xmldb:store("/db/test", "test.xml", $npt:DATA)
};

declare
    %test:tearDown
function npt:cleanup() {
    xmldb:remove("/db/test")
};

declare
    %test:assertEquals("<c>correct</c>")
function npt:in-memory() {
    $npt:DATA//c[../preceding-sibling::a[1]/b = 'A']
};

declare
    %test:assertEquals("<c>correct</c>")
function npt:in-database() {
    doc("/db/test.xml")//c[../preceding-sibling::a[1]/b = 'A']
};
```

The results:

```xml
<testsuites>
    <testsuite package="http://exist-db.org/test/nested-positional-predicate"
        timestamp="2018-05-29T23:01:23.898-05:00" failures="1" pending="0" tests="2" time="PT0.029S">
        <testcase name="in-database" class="npt:in-database">
            <failure
                message="assertEquals failed: wrong number of items returned by function. Expected: 1. Got: 0"
                type="failure-error-code-1">&lt;c&gt;correct&lt;/c&gt;</failure>
            <output/>
        </testcase>
        <testcase name="in-memory" class="npt:in-memory"/>
    </testsuite>
</testsuites>
```

### Context information

- eXist-db version + Git Revision hash: eXist-db 4.2.0-SNAPSHOT+201805281153 (e46c45c9d) 
- Java version: Oracle JDK 1.8.0_172-b11
- Operating system: macOS 10.13.4
- 32 or 64 bit: 64 bit
- Any custom changes in e.g. conf.xml: none
