### What is the problem

The `util:expand()` function alters the node types of attribute and document nodes instead of preserving their original node type.

### What did you expect

The function documentation describes the function as follows:

> Creates an in-memory copy of the passed node set, using the specified serialization options. 
> `$node` as `node()*` The node(s) to create in-memory copies of.
> Returns: `node()*` the results

Thus, this function should return an in-memory copy of the original node setâ€”and the node type of each passed node should be preserved in the output.

### Describe how to reproduce or add a test

The following query illustrates the problem:

```xquery
xquery version "3.1";

(: these correctly return true :)
util:expand( comment { "x" } ) instance of comment(),
util:expand( processing-instruction x { "" } ) instance of processing-instruction(),
util:expand( text { "x" } ) instance of text(),
util:expand( element x {()} ) instance of element(),

(: these incorrectly return false :)
util:expand( attribute x {""} ) instance of attribute(),
util:expand( document { element x {()} } ) instance of document-node(),

(: these incorrectly return true :)
util:expand( attribute x {""} ) instance of text(),
util:expand( document { element x {()} } ) instance of element()
```

The results shows that attribute nodes are being transformed into text nodes, and document nodes are being transformed into element nodes.

The following XQSuite (attached here: [util-expand-test.xql.zip](https://github.com/eXist-db/exist/files/1333602/util-expand-test.xql.zip)) also demonstrates the problem.

```xquery
xquery version "3.1";

module namespace ue="http://joewiz.org/ns/test/exist/util/expand";

declare namespace test="http://exist-db.org/xquery/xqsuite";

declare
    %test:assertTrue
function ue:attribute() as xs:boolean {
    util:expand( attribute foo { "" } ) instance of attribute()
};

declare
    %test:assertTrue
function ue:comment() as xs:boolean {
    util:expand( comment { "foo" } ) instance of comment()
};

declare
    %test:assertTrue
function ue:document() as xs:boolean {
    util:expand( document { element foo {()} } ) instance of document-node()
};

declare
    %test:assertTrue
function ue:element() as xs:boolean {
    util:expand( element foo {()} ) instance of element()
};

declare
    %test:assertTrue
function ue:pi() as xs:boolean {
    util:expand( processing-instruction foo { "" } ) instance of processing-instruction()
};

declare
    %test:assertTrue
function ue:text() as xs:boolean {
    util:expand( text { "foo" } ) instance of text()
};
```

The results, as of now:

```xml
<?xml version="1.0" encoding="UTF-8"?>
<testsuites>
    <testsuite package="http://joewiz.org/ns/test/exist/util/expand"
        timestamp="2017-09-26T10:51:00.805-04:00" failures="2" pending="0" tests="6" time="PT0.012S">
        <testcase name="attribute" class="ue:attribute">
            <failure message="assertExists failed." type="failure-error-code-1"/>
            <output>false</output>
        </testcase>
        <testcase name="comment" class="ue:comment"/>
        <testcase name="document" class="ue:document">
            <failure message="assertExists failed." type="failure-error-code-1"/>
            <output>false</output>
        </testcase>
        <testcase name="element" class="ue:element"/>
        <testcase name="pi" class="ue:pi"/>
        <testcase name="text" class="ue:text"/>
    </testsuite>
</testsuites>
```

### Context information

- eXist-db version + Git Revision hash: 3.5.0-SNAPSHOT+201709261032 bbc8f8d08 
- Java version: 1.8.0_144
- Operating system: macOS 10.13
- 32 or 64 bit: 64 bit
- Any custom changes in e.g. conf.xml: none
