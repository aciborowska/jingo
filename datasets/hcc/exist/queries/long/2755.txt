### What is the problem
`util:expand($result)` has no exist:match elements

### What did you expect
`$result` to have at least one exist:match elements

### Describe how to reproduce or add a test
The following test fails as no exist:match nodes are found in the util:expand($result)
However you can remove the line of configuration from collection.xconf marked `<!-- remove to pass the test -->` and it will pass the test.
This indicates that the order of the <text/> nodes in the collection.xconf configuration impacts on the result of util:expand(). Presumably this is not as it should be?

```xquery
xquery version "3.1";

module namespace eftt="http://exist-db.org/xquery/lucene/eft/test";

declare namespace eft="http://read.84000.co/ns/1.0";
declare namespace test="http://exist-db.org/xquery/xqsuite";

declare variable $eftt:test :=
    <test xmlns="http://read.84000.co/ns/1.0">
        <phrase>All phenomena are devoid of independent existence</phrase>
    </test>;

declare variable $eftt:xconf :=
    <collection xmlns="http://exist-db.org/collection-config/1.0">
        <index xmlns:eft="http://read.84000.co/ns/1.0">
            <lucene>
                <analyzer id="st" class="org.apache.lucene.analysis.standard.StandardAnalyzer"/>
                <analyzer id="ws" class="org.apache.lucene.analysis.core.WhitespaceAnalyzer"/>
                <text match="//eft:phrase" analyzer="st" field="phrase-st"/><!-- remove to pass the test -->
                <text match="//eft:phrase" analyzer="ws" field="phrase-ws"/>
            </lucene>
        </index>
    </collection>;

declare %test:setUp function eftt:setup() {
    xmldb:create-collection("/db/system/config/db", "test"),
    xmldb:store("/db/system/config/db/test", "collection.xconf", $eftt:xconf),
    xmldb:create-collection("/db", "test"),
    xmldb:store("/db/test", "test.xml", $eftt:test)
};

declare %test:tearDown function eftt:teardown() {
    xmldb:remove("/db/test"),
    xmldb:remove("/db/system/config/db/test")
};

declare %test:assertExists function eftt:ft-result-contains-match() {
    let $test-data := collection('/db/test')
    for $result in $test-data//eft:phrase[ft:query-field('phrase-ws', 'of')]
    return
       util:expand($result)//exist:match
};
```
### Context information
- eXist-db version 4.6.1
- java version "1.8.0_111"
- Ubuntu 16.04
- 64 bit
- Installed from eXist-db-setup-4.6.1.jar