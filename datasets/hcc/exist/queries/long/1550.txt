### What is the problem
Starting with eXist version 3.4.0 inline functions within maps will not inherit variables declared outside its function body. The problem has been discussed on [eXist-open](https://sourceforge.net/p/exist/mailman/message/36045046/) and was confirmed by @joewiz by testing against Saxon and BaseX.

### What did you expect

I expected all three tests to pass without errors (as they do in eXist version 3.3.0).

### Describe how to reproduce or add a test

```xquery
xquery version "3.1";

module namespace ct="http://exist-db.org/xquery/closures/test";

declare variable $ct:model := function($param) {
    let $func := function() {
       $param
    }
    return
        map {
            'test1' : $func,
            'test2' : $func(),
            'test3' : function() {
                $param
            }
        }
};

declare %test:assertEquals('Hello World') function ct:test1-function-reference() as xs:string {
    try {
        $ct:model('Hello World')?test1()
    }
    catch * {$err:code || ' ' || $err:description}
};

declare %test:assertEquals('Hello World') function ct:test2-function-call() as xs:string {
    try {
        $ct:model('Hello World')?test2
    }
    catch * {$err:code || ' ' || $err:description}
};

declare %test:assertEquals('Hello World') function ct:test3-inline-function() as xs:string {
    try {
        $ct:model('Hello World')?test3()
    }
    catch * {$err:code || ' ' || $err:description}
};
```


### Context information
- eXist-db version 3.5.0-SNAPSHOT /  fdaabad
- Java 1.8.0_131 [Oracle Corporation]
- Operating system MacOs Capitan 64 bit
- vanilla build from develop branch
