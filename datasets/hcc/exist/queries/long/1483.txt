### What is the problem

eXist does not take the `item-separator` serialization option into account for methods the serialization spec says it is relevant for.

### What did you expect

According to https://www.w3.org/TR/xslt-xquery-serialization-31/, the `item-separator` serialization option is active for methods `xml`, `html`, `xhtml`, and `text`. 

(eXist does support this option for the recently-added `adaptive` method. See test at https://github.com/eXist-db/exist/blob/8a2ec090613854c71ab079b66fd2cf5755d97896/test/src/xquery/xquery3/serialize.xql#L334-L340.)

(Please consider closing https://github.com/eXist-db/exist/issues/888 as this is a more specific and complete description of the issue the OP was after.)

### Describe how to reproduce or add a test

Here is a xqsuite test:

```xquery
xquery version "3.1";

module namespace ist="http://exist-db.org/xquery/serialization/item-separator";

declare namespace output="http://www.w3.org/2010/xslt-xquery-serialization";
declare namespace test="http://exist-db.org/xquery/xqsuite";

declare function ist:serialize($data as item()*, $method as xs:string) {
    let $options :=
        <output:serialization-parameters>
            <output:method>{$method}</output:method>
            <output:item-separator>--</output:item-separator>
        </output:serialization-parameters>
    return
        serialize($data, $options)
};

declare variable $ist:test := (1, 2);

declare %test:assertEquals("1--2") function ist:item-separator-text-method() {
    ist:serialize($ist:test, "text")
};

declare %test:assertEquals("1--2") function ist:item-separator-html-method() {
    ist:serialize($ist:test, "html")
};

declare %test:assertEquals("1--2") function ist:item-separator-xhtml-method() {
    ist:serialize($ist:test, "xhtml")
};

declare %test:assertEquals("1--2") function ist:item-separator-xml-method() {
    ist:serialize($ist:test, "xml")
};

declare %test:assertEquals("1--2") function ist:item-separator-adaptive-method() {
    ist:serialize($ist:test, "adaptive")
};
```

Currently, only the last of these tests passes.

### Context information

- eXist-db version + Git Revision hash: 3.4.0-SNAPSHOT+201707142259 (af161fd52) 
- Java version: 1.8.0_131
- Operating system: macOS 10.12.5
- 32 or 64 bit: 64 bit
- Any custom changes in e.g. conf.xml: No
